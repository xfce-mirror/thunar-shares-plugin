dnl
dnl Copyright (c) 2009-2010
dnl         Daniel Morales <daniel@daniel.com.uy>
dnl

dnl -------------------------------------------------------
dnl Version
dnl -------------------------------------------------------
m4_define([tsp_version_major], [0])
m4_define([tsp_version_minor], [2])
m4_define([tsp_version_micro], [1])
m4_define([tsp_version_build], [@REVISION@])
m4_define([tsp_version_tag], [git])
m4_define([tsp_version], [tsp_version_major().tsp_version_minor().tsp_version_micro()ifelse(tsp_version_tag(),[],[], [-ifelse(tsp_version_tag(), [git], [tsp_version_tag()-tsp_version_build()], [tsp_version_tag()])])])

dnl -------------------------------------------------------
dnl Debugging support for GIT
dnl -------------------------------------------------------
m4_define([tsp_debug_default], [ifelse(tsp_version_tag(), [git], [full], [minimum])])

dnl -------------------------------------------------------
dnl Init autoconf
dnl -------------------------------------------------------
AC_COPYRIGHT([Copyright (c) 2009-2010 Daniel Morales <daniel@daniel.com.uy>.])
AC_INIT([thunar-shares-plugin], [tsp_version], [thunar-dev@xfce.org])
AC_CONFIG_SRCDIR([thunar-plugin/thunar-shares-plugin.c])
AC_PREREQ(2.59)
AC_CANONICAL_TARGET()

dnl -------------------------------------------------------
dnl Init automake
dnl -------------------------------------------------------
AM_INIT_AUTOMAKE([1.9])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE

AC_ISC_POSIX
AC_HEADER_STDC

dnl -------------------------------------------------------
dnl Programs
dnl -------------------------------------------------------
AC_PROG_CC
AC_PROG_LD
AC_PROG_INSTALL
AC_PROG_LIBTOOL
AC_PROG_INTLTOOL([0.31])

dnl -------------------------------------------------------
dnl Check headers
dnl -------------------------------------------------------
AC_CHECK_HEADERS([string.h sys/types.h sys/wait.h time.h sys/stat.h unistd.h]) 

dnl -------------------------------------------------------
dnl Packages checks
dnl -------------------------------------------------------
XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.12.0])
XDT_CHECK_PACKAGE([GLIB], [glib-2.0], [2.18.0])
XDT_CHECK_PACKAGE([THUNARX], [thunarx-2], [1.0.1])

dnl -------------------------------------------------------
dnl Languages
dnl -------------------------------------------------------
XDT_I18N([@LINGUAS@])

dnl -------------------------------------------------------
dnl Debugging support
dnl -------------------------------------------------------
AC_ARG_ENABLE([debug],
AC_HELP_STRING([--enable-debug=@<:@no/minimum/yes/full@:>@], [Turn on debugging @<:@default=tsp_debug_default@:>@]),
  [], [enable_debug=tsp_debug_default])
AC_MSG_CHECKING([whether to enable debugging support])
if test x"$enable_debug" = x"full" -o x"$enable_debug" = x"yes"; then
  dnl Check whether the compiler accepts the -fno-strict-aliasing
  dnl switch, as certain packages - like Glib - still dont work
  dnl properly with newer compilers.
  save_CFLAGS="$CFLAGS"
  CFLAGS="$CFLAGS -fno-strict-aliasing"
  AC_MSG_CHECKING([whether $CC accepts -fno-strict-aliasing])
  AC_COMPILE_IFELSE(AC_LANG_SOURCE([int x;]), [
    AC_MSG_RESULT([yes])
    PLATFORM_CFLAGS="$PLATFORM_CFLAGS -fno-strict-aliasing"
  ], [
    AC_MSG_RESULT([no])
  ])
  CFLAGS="$save_CFLAGS"

  dnl Make sure we detect possible errors
  PLATFORM_CFLAGS="$PLATFORM_CFLAGS -Werror -Wall"

  dnl Paranoia for --enable-debug=full
  if test x"$enable_debug" = x"full"; then
    PLATFORM_CFLAGS="$PLATFORM_CFLAGS -O0 -g3"
    PLATFORM_CPPFLAGS="$PLATFORM_CPPFLAGS -DG_DISABLE_DEPRECATED -DG_ENABLE_DEBUG"
  fi
else
  dnl Disable object cast checks
  PLATFORM_CPPFLAGS="$PLATFORM_CPPFLAGS -DG_DISABLE_CAST_CHECKS"

  dnl Disable all checks for --enable-debug=no
  if test x"$enable_debug" = x"no"; then
    PLATFORM_CPPFLAGS="$PLATFORM_CPPFLAGS -DG_DISABLE_ASSERT -DG_DISABLE_CHECKS"
  fi
fi

dnl -------------------------------------------------------
dnl Check for linker optimizations
dnl -------------------------------------------------------
AC_MSG_CHECKING([whether $LD accepts -O1])
case `$LD -O1 -v 2>&1 </dev/null` in
*GNU* | *'with BFD'*)
  PLATFORM_LDFLAGS="$PLATFORM_LDFLAGS -Wl,-O1"
  AC_MSG_RESULT([yes])
  ;;
*)
  AC_MSG_RESULT([no])
  ;;
esac

dnl -------------------------------------------------------
dnl Export vars
dnl -------------------------------------------------------
AC_SUBST(PLATFORM_CPPFLAGS)
AC_SUBST(PLATFORM_CFLAGS)
AC_SUBST(PLATFORM_LDFLAGS)

AC_OUTPUT([
	Makefile
	po/Makefile.in
	libshares/Makefile
	thunar-plugin/Makefile
])

echo
echo "Prefix:        $prefix"
echo "Debug Support: $enable_debug"
echo
